#pragma kernel AddParticles
#pragma kernel Prepare
#pragma kernel ProcessInteraction_Impulse
#pragma kernel ProcessInteraction_SPH_Pass1
#pragma kernel ProcessInteraction_SPH_Pass2
#pragma kernel ProcessGBufferCollision
#pragma kernel ProcessColliders
#pragma kernel ProcessForces
#pragma kernel Integrate

#include "ParticleDataType.cginc"
#define BLOCK_SIZE 512

StructuredBuffer<WorldData>			world_data;
StructuredBuffer<Cell>				cells;
RWStructuredBuffer<Particle>		particles;
StructuredBuffer<Particle>			particles_to_add;
RWStructuredBuffer<ParticleIData>	pimd;
Texture2D							gbuffer_position;
Texture2D							gbuffer_normal;
StructuredBuffer<SphereCollider>	sphere_colliders;
StructuredBuffer<CapsuleCollider>	capsule_colliders;
StructuredBuffer<BoxCollider>		box_colliders;
StructuredBuffer<Force>				forces;

SamplerState samplergbuffer_position
{
	Filter   = MIN_MAG_MIP_POINT;
	AddressU = Clamp;
	AddressV = Clamp;
};
SamplerState samplergbuffer_normal
{
	Filter   = MIN_MAG_MIP_POINT;
	AddressU = Clamp;
	AddressV = Clamp;
};

bool IsOverlaped(float3 pos, AABB aabb)
{
	float3 wext = world_data[0].world_extent + world_data[0].particle_size;
	float3 rpos = pos-aabb.center;
	if(abs(rpos).x>wext.x || abs(rpos).y>wext.y || abs(rpos).z>wext.z)
	{
		return false;
	}
	return true;
}

int3 DecodeIndex(uint i)
{
	int3 div = world_data[0].world_div;
	int3 div_bits = world_data[0].world_div_bits;
	int3 r = 0;
	r.x = i & (div.x-1);
	r.y = i>>(div_bits.x) & (div.y-1);
	r.z = i>>(div_bits.x+div_bits.y) & (div.z-1);
	return r;
}



[numthreads(BLOCK_SIZE,1,1)]
void AddParticles(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;

	particles[world_data[0].num_particles+tid] = particles_to_add[tid];
}


[numthreads(BLOCK_SIZE,1,1)]
void Prepare(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;
	pimd[tid].accel = 0.0;
}


[numthreads(BLOCK_SIZE,1,1)]
void ProcessInteraction_Impulse(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;

	Cell cell = cells[tid];
	if(cell.end==0) { return; }

	float radius = world_data[0].particle_size;
	float radius2 = radius*2.0;
	float stiffness = world_data[0].pressure_stiffness;
	int3 shift = world_data[0].world_div_shift;
	int3 idx = DecodeIndex(tid);
	int3 nbeg = max(idx-1, 0);
	int3 nend = min(idx+1, world_data[0].world_div-1);

	for(int i=cell.begin; i<cell.end; ++i) {
		float3 pos1 = particles[i].position;
		float3 accel = 0.0;
		for(int nzi=nbeg.z; nzi<=nend.z; ++nzi) {
		for(int nyi=nbeg.y; nyi<=nend.y; ++nyi) {
		for(int nxi=nbeg.x; nxi<=nend.x; ++nxi) {
			Cell ngd = cells[dot(int3(nxi,nyi,nzi), shift)];
			for(int t=ngd.begin; t<ngd.end; ++t) {
				float3 pos2 = particles[t].position;
				float3 diff = pos1-pos2;
				float3 dir = normalize(diff);
				float distance = length(diff);
				if(distance>0.0) { // prevent self-collision
					float penetration = min(distance-radius2, 0.0);
					accel += dir * -penetration * stiffness*0.25;
				}
			}
		}}}

		pimd[i].accel += accel;
		//pimd[i].accel.y += 10.5;
	}
}


[numthreads(BLOCK_SIZE,1,1)]
void ProcessInteraction_SPH_Pass1(uint3 dtid : SV_DispatchThreadID)
{
	// todo
}

[numthreads(BLOCK_SIZE,1,1)]
void ProcessInteraction_SPH_Pass2(uint3 dtid : SV_DispatchThreadID)
{
	// todo
}


[numthreads(BLOCK_SIZE,1,1)]
void ProcessGBufferCollision(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;
	if(particles[tid].lifetime==0.0) { return; }
	WorldData wd = world_data[0];

	float3 ppos = particles[tid].position;

	float4 tpos4 = mul(wd.view_proj, float4(ppos, 1.0));
	float2 tpos = (tpos4.xy/tpos4.w + 1.0) * 0.5;
	tpos = tpos*wd.rt_size;

	float4 fpos4 = gbuffer_position[tpos];
	if(fpos4.w!=0.0) {
		float3 fnormal = gbuffer_normal[tpos].xyz;
		float r = 0.2;
		//float penetration = min(fpos4.w-tpos4.z-wd.particle_size, 0.0);
		float penetration = min(length(fpos4.xyz-ppos.xyz)-wd.particle_size-r, 0.0);
		{
			pimd[tid].accel += fnormal * (-penetration * wd.wall_stiffness);
		}
	}
}


[numthreads(BLOCK_SIZE,1,1)]
void ProcessColliders(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;
	if(particles[tid].lifetime==0.0) { return; }
	WorldData wd = world_data[0];

	float3 ppos = particles[tid].position;
	float3 paccel = pimd[tid].accel;
	int hit = -1;
	int i;

	// process sphere colliders
	for(i=0; i<wd.num_sphere_colliders; ++i) {
		SphereCollider col = sphere_colliders[i];
		if(!IsOverlaped(ppos, col.info.aabb)) { continue; }

		float3 diff = ppos - col.shape.center;
		float radius_sum = col.shape.radius + wd.particle_size;
		float penetration = min(length(diff)-radius_sum, 0.0f);
		float3 dir = normalize(diff);
		paccel += dir * -penetration * wd.wall_stiffness;
		if(penetration<0.0) {
			hit = col.info.owner_objid;
		}
	}

	// process capsule colliders
	for(i=0; i<wd.num_capsule_colliders; ++i) {
		CapsuleCollider col = capsule_colliders[i];
		if(!IsOverlaped(ppos, col.info.aabb)) { continue; }

		float3 pos1 = col.shape.pos1;
		float3 pos2 = col.shape.pos2;
		float3 d = pos2-pos1;
		float t = dot(ppos-pos1, pos2-pos1) / dot(d,d);
		float3 diff;
		if(t<=0.0f) {
			diff = ppos-pos1;
		}
		else if(t>=1.0f) {
			diff = ppos-pos2;
		}
		else {
			float3 nearest = pos1 + (pos2-pos1)*t;
			diff = ppos-nearest;
		}
		float radius_sum = col.shape.radius + wd.particle_size;
		float penetration = min(length(diff)-radius_sum, 0.0f);
		float3 dir = normalize(diff);
		paccel += dir * -penetration * wd.wall_stiffness;
		if(penetration<0.0) {
			hit = col.info.owner_objid;
		}
	}

	// process box colliders
	for(i=0; i<wd.num_box_colliders; ++i) {
		BoxCollider col = box_colliders[i];
		if(!IsOverlaped(ppos, col.info.aabb)) { continue; }

		int inside = 0;
		float closest_distance = -999.0f;
		float3 closest_normal;
		float3 rpos = ppos - col.shape.center;
		for(int p=0; p<6; ++p) {
			float3 plane_normal = col.shape.planes[p].normal;
			float plane_distance = col.shape.planes[p].distance;
			float distance = dot(rpos, plane_normal) + (plane_distance - wd.particle_size);
			if(distance < 0.0f) {
				inside++;
				if(distance > closest_distance) {
					closest_distance = distance;
					closest_normal = plane_normal;
				}
			}
		}
		if(inside==6) {
			paccel += closest_normal * -closest_distance * wd.wall_stiffness;
			hit = col.info.owner_objid;
		}
	}

	pimd[tid].accel = paccel;
	particles[tid].hit_objid = hit;
}


[numthreads(BLOCK_SIZE,1,1)]
void ProcessForces(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;
	WorldData wd = world_data[0];

	pimd[tid].accel.y -= wd.gravity;
}

[numthreads(BLOCK_SIZE,1,1)]
void Integrate(uint3 dtid : SV_DispatchThreadID)
{
	uint tid = dtid.x;
	if(particles[tid].lifetime==0.0) { return; }
	WorldData wd = world_data[0];

	float3 ppos = particles[tid].position;
	float3 pvel = particles[tid].velocity + (pimd[tid].accel * wd.timestep);
	pvel *= wd.decelerate;
	ppos += pvel * wd.timestep;

	{
		float3 relpos = ppos - wd.world_center;
		if(	abs(relpos).x>wd.world_extent.x ||
			abs(relpos).y>wd.world_extent.y ||
			abs(relpos).z>wd.world_extent.z)
		{
			particles[tid].lifetime = 0.0;
		}
	}

	particles[tid].position = ppos;
	particles[tid].velocity = pvel;
	particles[tid].speed = length(pvel);
	particles[tid].lifetime = max(particles[tid].lifetime-wd.timestep, 0.0);
}
